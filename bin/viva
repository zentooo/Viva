#!/usr/bin/env perl

use strict;
use warnings;

use autodie;

use Getopt::Compact::WithCmd;

use Path::Class;
use File::Temp qw/tempdir/;
use File::Copy::Recursive qw/dircopy/;
use File::Path qw/rmtree/;
use File::Inplace;
use File::chdir;

use Cwd;
use Term::ANSIColor qw/:constants/;

use LWP;
use JSON;
use Data::Util qw/:check/;


my $go = Getopt::Compact::WithCmd->new( modes => [ qw/verbose/ ],
  global_struct => [
    [ [qw/d dest/], 'destination directory (vim home)', '=s', \my $vim_home, { default => "$ENV{HOME}/.vim"} ]
  ],
  command_struct => {
    init => {
      desc => 'initialize pathogen + git submodule vim plugin management environment.'
    },
    search => {
      desc => 'search and install vim plugins by github',
      options => [ [ [qw/l language/], 'language such as Perl, Ruby, Python, Scheme (default is VimL)', '=s', \my $lang, { default => "VimL"} ] ],
    },
    list => {
      desc => 'list all installed plugins'
    },
    remove => {
      desc => 'remove installed plugin'
    },
    update => {
      desc => 'update all installed plugins'
    },
    cloned => {
      desc => 'use this command just after you cloned from remote repo'
    },
  }
);

my $opts = $go->opts;
my $cmd = $go->command || $go->show_usage;

&main; exit;

sub main {
  __PACKAGE__->can($cmd)->();
}


# init

sub init {

  if ( -e "$vim_home/autoload/pathogen.vim" ) {
    print "Looks you already have install pathogen.vim :)\n";
  }
  else {
    my $tmpdir = tempdir(CLEANUP => 1);
    system("git clone git://github.com/tpope/vim-pathogen.git $tmpdir/pathogen");
    dircopy("$tmpdir/pathogen", $vim_home);
    rmtree "$vim_home/.git";

    print "\n";
    print "###################################################################################\n";
    print "not forget to add `call pathogen#runtime_append_all_bundles` line to your .vimrc :)\n";
    print "###################################################################################\n";
    print "\n";
  }
}


# search -> install

sub search {
  my $query = shift @ARGV;
  return unless $query;
  my $url = "http://github.com/api/v2/json/repos/search/$query?language=$lang";
  my $ua = LWP::UserAgent->new;
  my $req = HTTP::Request->new(GET => $url);
  my $res = $ua->request($req);

  if ( $res->is_success ) {

    my $json = decode_json($res->content);
    my $repositories = $json->{repositories};

    if ( is_array_ref($repositories) ) {
      $repositories = [sort { $b->{followers} <=> $a->{followers} } @$repositories];
      my $repos_num = _print_repos($repositories);

      return if $repos_num == 0;
      _run_term($repositories, $repos_num);
    }
    else {
      die "github response includes error, please try it again after a while: $json->{error}->{error}";
    }
  }
  else {
    die "github returns http error, please try it again after a while: $res->code";
  }
}

sub _print_repos {
  my $repositories = shift;

  return 0 if scalar @$repositories == 0;

  $Term::ANSIColor::AUTORESET = 1;
  my $repos_num = 1;

  for my $repository (@$repositories) {

    next if $repository->{name} =~ /^\.?vim$|(dot|vim)files|dotvim|conf(ig)?/i;

    print BOLD WHITE ON_BLUE $repos_num;
    print " ";
    print BOLD WHITE "$repository->{username}/";
    print BOLD CYAN $repository->{name};
    print " (";
    print BOLD GREEN "followers: ";
    print "$repository->{followers},";
    print " ";
    print BOLD GREEN "pushed: ";
    $repository->{pushed} =~ s/T.*$//;
    print "$repository->{pushed},";
    print " ";
    print BOLD GREEN "url: ";
    print "http://github.com/$repository->{username}/$repository->{name}";
    print ")\n";
    print " " x 6;
    print $repository->{description};
    print "\n";
    $repos_num++;
  }

  print "\n";
  print BOLD YELLOW "==> ";
  print BOLD WHITE "Enter n (seperated by blanks) of vim plugins to be installed\n";
  print BOLD YELLOW "==> ";

  return $repos_num;
}

sub _run_term {
  my ($repositories, $repos_num) = @_;

  my $command = <STDIN>;
  my @numbers = split / /, $command;

  for my $number (@numbers) {
    if ( $number =~ /\d+/ && grep { $_ == $number } ( 1 .. $repos_num ) ) {
      _install($repositories->[$number - 1]);
      print "\n";
    }
  }
}

sub _install {
  my $repository = shift;
  my $git_uri = "git://github.com/$repository->{username}/$repository->{name}.git";

  _home_do(sub {
    $vim_home =~ /$ENV{HOME}\/(.*)/;
    my $relative_vim_home = $1;
    system("git submodule add $git_uri $relative_vim_home/bundle/$repository->{name}");
    system("git submodule init");
  });
}


# remove

sub remove {
  my $name = shift @ARGV;
  return unless $name;

  if ( -d "$vim_home/bundle/$name" ) {
    my $gitmodules = "$ENV{HOME}/.gitmodules";
    my $gitconfig = "$ENV{HOME}/.git/config";
    _remove_submodule_info($name, $gitmodules);
    _remove_submodule_info($name, $gitconfig);

    _home_do(sub {
      system("git rm --cached $vim_home/bundle/$name");
      rmtree("$vim_home/bundle/$name");
    });
  }
  else {
    print "Looks you are not installing the plugin name with $name\n";
  }
}

sub _remove_submodule_info {
  my ($name, $filename) = @_;
  my $editor = File::Inplace->new(file => $filename, chomp => 0);

  while ( my $line = $editor->next_line ) {
    if ( $line =~ /bundle\/$name|$name\.git/ ) {
      $editor->replace_line('');
    }
  }
  $editor->commit;
}


# list

sub list {
  my @names;

  _home_do(sub {
    my @lines = `git submodule status`;

    for my $line (@lines) {
      my @parts = split / (?!\()/, $line;
      print $parts[-1];
    }
  });
}


# update

sub update {
  for my $dir (dir("$vim_home/bundle")->children) {
    _chdir_do($dir, sub {
      system('git pull -f');
    });
  }
}

# cloned

sub cloned {
  _home_do(sub {
    system('git init');
    system('git update');
  });
  for my $dir (dir("$vim_home/bundle")->children) {
    _chdir_do($dir, sub {
      system('git checkout master -f');
    });
  }
}


# util

sub _home_do {
  _chdir_do($ENV{HOME}, shift);
}

sub _chdir_do {
  my ($dest, $code) = @_;
  {
    local $CWD = $dest;
    $code->();
  }
}
